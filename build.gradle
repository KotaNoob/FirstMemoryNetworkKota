import org.apache.tools.ant.filters.*

apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

group = 'me.kota'
version = '1.0-SNAPSHOT'

ext.pluginName = 'FirstMemoryNetwork-Kota'
ext.author = 'Kota'
ext.mainPackage = 'me.kota.firstmemorynetworkkota'
ext.mainClass = mainPackage + ".Main"

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    ext.kotlin_version = '1.5.10'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    maven { url 'https://jitpack.io/' }
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = uri("https://maven.pkg.github.com/Firstmemory-Network/RPGItemAPI")
        credentials {
            username = 'KotaNoob'
            password = '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'
        }
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}
compileKotlin {
    kotlinOptions.javaParameters = true
}

dependencies {
    api 'me.kota:rpgitemapi:1.02'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

configurations {
    testCompile.extendsFrom compileOnly
}

shadowJar {
    relocate 'co.aikar.commands', "${mainPackage}.acf"
}
build { dependsOn shadowJar }

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "pluginName"     : pluginName,
            "project.version": project.property("version"),
            "author"         : author,
            "mainClass"      : mainClass
    ]
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        target(MavenPublication) {
            groupId 'dev.firstmemory'
            artifactId 'rpgcore'
            version version
            from components.java
            artifact tasks.sourceJar
        }
    }
}